#!/usr/bin/env -S sed -f

# add initial cube state
1 {
x
s/$/ABCDEFGHIJKLMNOPQRSTUVWX/
x
}



# convert   R F2 D' -> RFFDDD

s/ //g
s/\(.\)2/\1\1/g
s/\(.\)'/\1\1\1/g



:move

/^$/ b done


# speffz corners:
#     A.B
#     . .
#     D.C
# E.F I.J M.N Q.R
# . . . . . . . .
# H.G L.K P.O T.S
#     U.V
#     . .
#     X.W

/^U/ {
x
#   A    B    C    D    D A B C
s/\(.\)\(.\)\(.\)\(.\)/\4\1\2\3/
#           E           I           M           Q      I   M   Q   E
s/\(....\)\(.\)\(...\)\(.\)\(...\)\(.\)\(...\)\(.\)/\1\4\3\6\5\8\7\2/
#            F           J           N           R      J   N   R   F
s/\(.....\)\(.\)\(...\)\(.\)\(...\)\(.\)\(...\)\(.\)/\1\4\3\6\5\8\7\2/
b next
}


/^D/ {
x
#              U    V    W    X      X U V W
s/\(.\{20\}\)\(.\)\(.\)\(.\)\(.\)/\1\5\2\3\4/
#             H           L           P           T      T   H   L   P
s/\(.\{7\}\)\(.\)\(...\)\(.\)\(...\)\(.\)\(...\)\(.\)/\1\8\3\2\5\4\7\6/
#             G           K           O           S      S   G   K   O
s/\(.\{6\}\)\(.\)\(...\)\(.\)\(...\)\(.\)\(...\)\(.\)/\1\8\3\2\5\4\7\6/
b next
}


/^R/ {
x
#              M    N    O    P      P M N O
s/\(.\{12\}\)\(.\)\(.\)\(.\)\(.\)/\1\5\2\3\4/
#        B               J              T         V      J   V   B   T
s/\(.\)\(.\)\(.......\)\(.\)\(.\{9\}\)\(.\)\(.\)\(.\)/\1\4\3\8\5\2\7\6/
#         C               K              Q              W      K   W   C   Q
s/\(..\)\(.\)\(.......\)\(.\)\(.\{5\}\)\(.\)\(.\{5\}\)\(.\)/\1\4\3\8\5\2\7\6/
b next
}


/^L/ {
x
#           E    F    G    H      H E F G
s/\(....\)\(.\)\(.\)\(.\)\(.\)/\1\5\2\3\4/
#          D               L             R             X      R   D   X   L
s/\(...\)\(.\)\(.......\)\(.\)\(.....\)\(.\)\(.....\)\(.\)/\1\6\3\2\5\8\7\4/
#   A              I              S         U    S   A   U   I
s/\(.\)\(.\{7\}\)\(.\)\(.\{9\}\)\(.\)\(.\)\(.\)/\5\2\1\4\7\6\3/
b next
}


/^F/ {
x
#             I    J    K    L      L I J K
s/\(.\{8\}\)\(.\)\(.\)\(.\)\(.\)/\1\5\2\3\4/
#          D           G             M              V     G   V   D   M
s/\(...\)\(.\)\(..\)\(.\)\(.....\)\(.\)\(.\{8\}\)\(.\)/\1\4\3\8\5\2\7\6/
#         C          F                 P            U      F   U   C   P
s/\(..\)\(.\)\(..\)\(.\)\(.........\)\(.\)\(....\)\(.\)/\1\4\3\8\5\2\7\6/
#
b next
}


/^B/ {
x
#              Q    R    S    T      T Q R S
s/\(.\{16\}\)\(.\)\(.\)\(.\)\(.\)/\1\5\2\3\4/
#   A              H             N              W    N   A   W   H
s/\(.\)\(......\)\(.\)\(.....\)\(.\)\(.\{8\}\)\(.\)/\5\2\1\4\7\6\3/
#        B          E              O              X      O   B   X   E
s/\(.\)\(.\)\(..\)\(.\)\(.\{9\}\)\(.\)\(.\{8\}\)\(.\)/\1\6\3\2\5\8\7\4/
}

:next
x
s/.//
b move



:done

x
h



# visualiser

# swap all right-to-left pairs
s/\(..\)\(.\)\(.\)/\1\3\2/g

#   ab    dc          uv    x    w
s/\(..\)\(..\)\(.*\)\(..\)\(.\)\(.\)/    \1\n    \2\n\3\n    \4\n    \5\6/

#                       ef    hg    ij    lk    mn    po    qr    ts
s/\(    ..\n    ..\n\)\(..\)\(..\)\(..\)\(..\)\(..\)\(..\)\(..\)\(..\)/\1\2\4\6\8\n\3\5\7\9/



# 8 bit alternative palette: 732415
s/[ABCD]/\x1b[48;5;231m  \x1b[m/g
s/[EFGH]/\x1b[48;5;202m  \x1b[m/g
s/[IJKL]/\x1b[48;5;34m  \x1b[m/g
s/[MNOP]/\x1b[48;5;196m  \x1b[m/g
s/[QRST]/\x1b[48;5;21m  \x1b[m/g
s/[UVWX]/\x1b[48;5;220m  \x1b[m/g
p

g
d
